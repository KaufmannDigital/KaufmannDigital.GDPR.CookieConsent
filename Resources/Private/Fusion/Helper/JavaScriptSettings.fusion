// renders a <script> with cookie settings
prototype(KaufmannDigital.GDPR.CookieConsent:Helper.JavaScriptSettings) < prototype(Neos.Fusion:Tag) {
    tagName = 'script'

    cookieSettingsNode = ${q(site).find('[instanceof KaufmannDigital.GDPR.CookieConsent:Content.CookieSettings]').get(0)}
    @context.cookieSettingsNode = ${this.cookieSettingsNode}
    @context.versionDate = ${q(this.cookieSettingsNode).property('versionDate')}
    @context.decisionTtl = ${q(this.cookieSettingsNode).property('decisionTtl')}
    @context.excludedDocuments = ${q(this.cookieSettingsNode).property('excludedDocuments')}

    @context.apiUrl = Neos.Fusion:UriBuilder {
        package = 'KaufmannDigital.GDPR.CookieConsent'
        controller = 'Api'
        action = 'renderCookieSettings'
        arguments.siteNode = ${site}
        absolute = true
    }

    @context.dimensionsIdentifier = Neos.Fusion:Value {
        consentDimensions = ${Configuration.setting('KaufmannDigital.GDPR.CookieConsent.consentDimensions')}
        dimensionValues = ${Array.filter(site.dimensionSpacePoint.coordinates, (value,key) => Array.indexOf(this.consentDimensions, key) >= 0)}
        value = ${Array.join(this.dimensionValues, '_')}
    }
    @context.cookieName = ${Configuration.setting('KaufmannDigital.GDPR.CookieConsent.cookieName')}
    @context.cookieDomainName = ${Configuration.setting('KaufmannDigital.GDPR.CookieConsent.cookieDomainName')}
    @context.hideBeforeInteraction = ${Configuration.setting('KaufmannDigital.GDPR.CookieConsent.hideBeforeInteraction')}

    @context.documentNodeDisabled = ${
        Array.indexOf(Configuration.setting('KaufmannDigital.GDPR.CookieConsent.excludeDocumentNodeTypes'), documentNode.nodeType.name) >= 0 ||
        (q(this.cookieSettingsNode).property('excludedDocuments') && Array.length(Array.filter(q(this.cookieSettingsNode).property('excludedDocuments'), (value,key) => value.identifier == documentNode.identifier)) > 0)
    }

    @context.acceptConfiguration = Neos.Fusion:DataStructure {
        parameterAccept = ${Configuration.setting('KaufmannDigital.GDPR.CookieConsent.parameterConsent')}
    }

    @context.cookieLifetime = ${Configuration.setting('KaufmannDigital.GDPR.CookieConsent.cookieLifetime')}

    @context.generatedJsUrl = Neos.Fusion:UriBuilder {
        package = 'KaufmannDigital.GDPR.CookieConsent'
        controller = 'JavaScript'
        action = 'renderJavaScript'
        arguments.site = ${site}
        absolute = true
    }

    content = ${"
            var KD_GDPR_CC = {
                apiUrl: '" + apiUrl + "',
                generatedJsUrl: '" + generatedJsUrl + "',
                cookieName: '" + cookieName +"',
                cookieLifetime: '" + cookieLifetime +"',
                cookieDomainName: '" + cookieDomainName +"',
                versionTimestamp: " + versionDate.timestamp * 1000 + ",
                decisionTtl: " + (decisionTtl ? (decisionTtl * 1000) : 0) + ",
                dimensionsIdentifier: '" + (dimensionsIdentifier != '' ? dimensionsIdentifier : 'default') + "',
                hideBeforeInteraction: " + (hideBeforeInteraction ? 'true' : 'false') +",
                documentNodeDisabled: " + (documentNodeDisabled ? 'true' : 'false') +",
                acceptConfiguration: " + Json.stringify(acceptConfiguration) + ",
                gtmConsentKeys: " + Json.stringify(GTMConsents.getAllConfiguredConsents(site)) + "
            };
        "}

    @if.condition = ${renderingMode.isEdit == false}

    @cache {
        mode = 'cached'
        entryIdentifier {
            name = 'KD_GDPR_JS_SETTINGS'
            cookieSettingsNode = ${Neos.Caching.entryIdentifierForNode(cookieSettingsNode)}
        }
        entryTags {
            1 = ${Neos.Caching.nodeTypeTag('KaufmannDigital.GDPR.CookieConsent:Content.CookieSettings', site)}
            2 = ${Neos.Caching.descendantOfTag(cookieSettingsNode)}
        }
    }
}
