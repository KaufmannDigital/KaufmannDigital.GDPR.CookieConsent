
include: **/*.fusion

prototype(Neos.Neos:Page) {

    @context.versionDate = ${q(site).find('[instanceof KaufmannDigital.GDPR.CookieConsent:Content.CookieSettings]').first().property("versionDate")}
    @context.apiUrl = Neos.Fusion:UriBuilder {
        package = 'KaufmannDigital.GDPR.CookieConsent'
        controller = 'Api'
        action = 'renderCookieSettings'
        arguments.siteNode = ${site.contextPath}
        absolute = true
    }

    @context.dimensionsIdentifier = Neos.Fusion:Value {
        consentDimensions = ${Configuration.setting('KaufmannDigital.GDPR.CookieConsent.consentDimensions')}
        dimensionValues = ${Array.filter(site.context.targetDimensions, (value,key) => Array.indexOf(this.consentDimensions, key) >= 0)}
        value = ${Array.join(this.dimensionValues, '_')}
    }
    @context.cookieName = ${Configuration.setting('KaufmannDigital.GDPR.CookieConsent.cookieName')}



    jsVariablesTag = Neos.Fusion:Tag {
        tagName = 'script'
        content = ${"
            var KD_GDPR_CC = {
                apiUrl: '" + apiUrl + "',
                cookieName: '" + cookieName +"',
                versionTimestamp: " + versionDate.timestamp * 1000 + ",
                dimensionsIdentifier: '" + (dimensionsIdentifier != '' ? dimensionsIdentifier : 'default') + "'
            };
        "}
        @if.condition = ${node.context.inBackend == false}
        @position = 'before closingBodyTag'
    }

    initializeJs = Neos.Fusion:Tag {
        tagName = 'script'
        content = ${File.readFile('resource://KaufmannDigital.GDPR.CookieConsent/Public/JavaScript/Initialize.js')}

        @if.condition = ${node.context.inBackend == false}
        @position = 'before closingBodyTag'
    }

    generatedJS = Neos.Fusion:Tag {
        tagName = 'script'
        attributes.defer = true
        attributes.async = true
        attributes.type = 'text/javascript'
        attributes.src = Neos.Fusion:UriBuilder {
            package = 'KaufmannDigital.GDPR.CookieConsent'
            controller = 'JavaScript'
            action = 'renderJavaScript'
            format = 'js'
            arguments.dimensions = ${site.context.dimensions}
        }

        @if.condition = ${node.context.inBackend == false || Configuration.setting('KaufmannDigital.GDPR.CookieConsent.backend.includeGeneratedJs') == true}
        @position = 'before closingBodyTag'
    }
}

cookieConsentSettings = KaufmannDigital.GDPR.CookieConsent:Content.CookieSettings
