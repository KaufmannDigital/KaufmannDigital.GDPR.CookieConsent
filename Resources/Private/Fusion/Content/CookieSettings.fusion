// The actual cookie banner
prototype(KaufmannDigital.GDPR.CookieConsent:Content.CookieSettings) < prototype(Neos.Neos:ContentComponent) {
    editableHeadline = Neos.Neos:Editable {
        property = 'headline'
    }

    editableMainDescription = Neos.Neos:Editable {
        property = 'mainDescription'
    }

    editableIndividualSettingsDescription = Neos.Neos:Editable {
        property = 'individualSettingsDescription'
    }

    individualSettings = Neos.Neos:ContentCollection {
        nodePath = 'settingElements'
        @cache.mode = 'embed'
    }

    closeButtonEnabled = ${q(node).property('closeButtonEnabled')}

    labels = Neos.Fusion:DataStructure {
        acceptAll = ${q(node).property('acceptAllButtonLabel')}
        save = ${q(node).property('saveButtonLabel')}
        acceptNecessaryCookies = ${q(node).property('acceptNecessaryCookiesLabel')}
        openIndividualSettings = ${q(node).property('openIndividualSettingsLabel')}
        closeIndividualSettings = ${q(node).property('closeIndividualSettingsLabel')}
    }

    styles = Neos.Fusion:Join {
        buttonText = Neos.Fusion:Case {
            black {
                condition = ${q(node).property('buttonTextColor') == 'black'}
                renderer = '--cookieconsent-button-text-color: rgb(0 0 0);'
            }
            white {
                condition = ${q(node).property('buttonTextColor') == 'white'}
                renderer = '--cookieconsent-button-text-color: rgb(255 255 255);'
            }
            none {
                condition = true
                renderer = ''
            }
        }
        background = ${q(node).property('backgroundColor')}
        background.@process.testHex = ${String.pregMatch(value, "/^#(?:[0-9a-fA-F]{3,4}){1,2}$/") != null ? value : ""}
        background.@process.toVar = ${value && !String.isBlank(value) && '--cookieconsent-bg-color:'+value+';'}

        theme = ${q(node).property('themeColor')}
        theme.@process.testHex = ${String.pregMatch(value, "/^#(?:[0-9a-fA-F]{3,4}){1,2}$/") != null ? value : ""}
        theme.@process.toVar = ${value && !String.isBlank(value) && '--cookieconsent-theme-color:'+value+';'}

        width = ${q(node).property('maxWidth')}
        width.@process.toVar = ${value && !String.isBlank(value) && '--cookieconsent-width:'+value+'px;'}
    }

    renderer = afx`
        <div
            class="gdpr-cookieconsent-container"
            style={props.styles}
            style.@if.set={props.styles}
            tabindex="0"
        >
            <KaufmannDigital.GDPR.CookieConsent:Helper.JavaScriptSettings />
            <div class="gdpr-cookieconsent-settings">
                <div class="gdpr-cookieconsent-settings__content">
                    <div class="gdpr-cookieconsent-settings__content__info">
                        <KaufmannDigital.GDPR.CookieConsent:Component.Atom.Headline>
                            {props.editableHeadline}
                        </KaufmannDigital.GDPR.CookieConsent:Component.Atom.Headline>
                        <div class="gdpr-cookieconsent-settings__content__info__description gdpr-cookieconsent-settings__content__info__description--main">
                            <KaufmannDigital.GDPR.CookieConsent:Component.Atom.Text>
                                {props.editableMainDescription}
                            </KaufmannDigital.GDPR.CookieConsent:Component.Atom.Text>
                        </div>
                        <div class="gdpr-cookieconsent-settings__content__info__description gdpr-cookieconsent-settings__content__info__description--settings hidden">
                            <KaufmannDigital.GDPR.CookieConsent:Component.Atom.Text>
                                {props.editableIndividualSettingsDescription}
                            </KaufmannDigital.GDPR.CookieConsent:Component.Atom.Text>
                        </div>
                    </div>

                    <div class="gdpr-cookieconsent-settings__content__cta">
                        <KaufmannDigital.GDPR.CookieConsent:Component.Molecule.CTAs
                            acceptAllLabel={props.labels.acceptAll}
                            saveLabel={props.labels.save}
                            acceptNecessaryLabel={props.labels.acceptNecessaryCookies}
                            openIndividualSettingsLabel={props.labels.openIndividualSettings}
                            closeIndividualSettingsLabel={props.labels.closeIndividualSettings}
                        />
                    </div>

                    <div class="gdpr-cookieconsent-settings__content__settings hidden">
                        {props.individualSettings}
                    </div>

                    <KaufmannDigital.GDPR.CookieConsent:Component.Molecule.FooterLinks settingsNode={node}/>
                </div>

                <div class="gdpr-cookieconsent-settings__close">
                    <KaufmannDigital.GDPR.CookieConsent:Component.Atom.CloseButton
                        @if.condition={props.closeButtonEnabled}/>
                </div>
            </div>
            <KaufmannDigital.GDPR.CookieConsent:Helper.GeneratedStyles/>
            <KaufmannDigital.GDPR.CookieConsent:Helper.GeneratedJavaScript/>
        </div>
    `

    @cache {
        mode = 'uncached'

        context {
            1 = 'node'
            2 = 'documentNode'
            3 = 'site'
            4 = 'cookieValue'
        }
    }
}
