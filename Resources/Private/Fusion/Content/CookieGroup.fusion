prototype(KaufmannDigital.GDPR.CookieConsent:Content.CookieGroup) < prototype(Neos.Neos:ContentComponent) {

    editableHeadline = Neos.Neos:Editable {
        property = 'headline'
    }

    editableDescription = Neos.Neos:Editable {
        property = 'description'
    }

    cookieContentCollection = Neos.Neos:ContentCollection {
        attributes.class = 'gdpr-cookieconsent-setting-group__cookies hidden'
        nodePath = 'cookies'
    }

    identifier = ${q(node).property('identifier')}
    detailsAvailable = ${q(node).children('cookies').children().count() > 0 || node.context.inBackend}

    labels = Neos.Fusion:DataStructure {
        hideDetails = ${I18n.translate('KaufmannDigital.GDPR.CookieConsent:NodeTypes.Content.CookieGroup:hideDetails')}
    }

    groupClasses = Neos.Fusion:DataStructure {
        main = 'gdpr-cookieconsent-setting-group'
    }

    gtmConsents = ${Json.stringify(GTMConsents.getConsentsForNode(node))}

    renderer = afx`
        <div class={Array.join(props.groupClasses, ' ')}>
            <!-- Augment the Switch-Component with data, to keep the api compatible to older versions -->
            <div class="gdpr-cookieconsent-setting-group__switch" data-gtm-consents={props.gtmConsents}>
                <KaufmannDigital.GDPR.CookieConsent:Component.Atom.Switch
                    value={props.identifier}
                    label={props.editableHeadline}
                    variant="group"
                />
            </div>
            <div class="gdpr-cookieconsent-setting-group__description">
                {props.editableDescription}
            </div>
            {props.cookieContentCollection}
            <a href='#' class="gdpr-cookieconsent-setting-group__details gdpr-cookieconsent-setting-group__details--open" @if.available={props.detailsAvailable} >Details&nbsp;></a>
            <a href='#' class="gdpr-cookieconsent-setting-group__details gdpr-cookieconsent-setting-group__details--close hidden" @if.available={props.detailsAvailable} >{props.labels.hideDetails}</a>
        </div>
    `
}
