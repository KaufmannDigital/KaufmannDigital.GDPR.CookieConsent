prototype(KaufmannDigital.GDPR.CookieConsent:Content.CookieSettings.Renderer) < prototype(Neos.Fusion:Renderer) {
    renderer = Neos.Fusion:Array {

        overlay = Neos.Fusion:Tag {
            attributes.class = 'gdpr-cookieconsent-overlay'
        }
        cookieSettings = Neos.Fusion:Tag {
            attributes.class = 'gdpr-cookieconsent-settings'
            content = Neos.Fusion:Tag {
                attributes.class = 'gdpr-cookieconsent-settings__content'
                content = Neos.Fusion:Array {
                    info = Neos.Fusion:Tag {
                        attributes.class = 'gdpr-cookieconsent-settings__content__info'
                        content = Neos.Fusion:Array {
                            headline = Neos.Fusion:Tag {
                                tagName = 'h2'
                                content = ${props.editableHeadline}
                            }

                            mainDescription = Neos.Fusion:Tag {
                                tagName = 'div'
                                attributes.class = 'gdpr-cookieconsent-settings__content__info__main-description'
                                content = ${props.editableMainDescription}
                            }

                            individualSettingsDescription = Neos.Fusion:Tag {
                                tagName = 'div'
                                attributes.class = 'gdpr-cookieconsent-settings__content__info__settings-description'
                                content = ${props.editableIndividualSettingsDescription}
                            }

                        }
                    }

                    cta = Neos.Fusion:Tag {
                        attributes.class = 'gdpr-cookieconsent-settings__content__cta'
                        content = Neos.Fusion:Array {
                            acceptAll = Neos.Fusion:Tag {
                                tagName = 'button'
                                attributes.id = 'gdpr-cc-btn-accept'
                                attributes.class = 'button'
                                content = ${props.labels.acceptAll}
                            }

                            save = Neos.Fusion:Tag {
                                tagName = 'button'
                                attributes.id = 'gdpr-cc-btn-save'
                                attributes.class = 'button'
                                content = ${props.labels.save}
                            }

                            necessaryCookies = Neos.Fusion:Tag {
                                tagName = 'a'
                                attributes.id = 'gdpr-cc-btn-accept-necessary'
                                content = ${props.labels.acceptNecessaryCookiesLabel}
                                @if.condition = ${props.hasNecessaryGroup}
                            }
                        }
                    }

                    individualSettings = Neos.Fusion:Tag {
                        attributes.class = 'gdpr-cookieconsent-settings__content__settings'
                        content = ${props.individualSettings}
                    }

                    links = Neos.Fusion:Tag {
                        attributes.class = 'gdpr-cookieconsent-settings__content__links'
                        content = Neos.Fusion:Array {
                            imprintLink = Neos.Fusion:Tag {
                                tagName = 'a'
                                attributes.href = ${props.imprintLink}
                                content = ${props.labels.imprintLabel}
                            }

                            pipe = Neos.Fusion:Tag {
                                tagName = 'span'
                                content = ' | '
                            }


                            dataProtectionPolicy = Neos.Fusion:Tag {
                                tagName = 'a'
                                attributes.href = ${props.dataProtectionPolicyLink}
                                content = ${props.labels.dataProtectionPolicyLabel}
                            }
                        }
                    }

                    individualSettingsSwitch = Neos.Fusion:Tag {
                        attributes.class = 'gdpr-cookieconsent-settings__content__individual-switch'
                        content = Neos.Fusion:Array {
                            enableSwitch = Neos.Fusion:Tag {
                                tagName = 'a'
                                attributes.id = 'gdpr-cc-btn-individual-settings-enable'
                                content = ${props.labels.openIndividualSettingsLabel}
                            }

                            disableSwitch = Neos.Fusion:Tag {
                                tagName = 'a'
                                attributes.id = 'gdpr-cc-btn-individual-settings-disable'
                                content = ${props.labels.closeIndividualSettingsLabel}
                            }
                        }
                    }

                    css = Neos.Fusion:Tag {
                        tagName = 'style'
                        content = Neos.Fusion:Array {
                            packageFile = ${File.readFile('resource://KaufmannDigital.GDPR.CookieConsent/Public/Styles/Main.css')}
                            customFile = ${File.readFile(Configuration.setting('KaufmannDigital.GDPR.CookieConsent.customCSSFilepath'))}

                            siteFile.@if.isSet = ${String.length(Configuration.setting('KaufmannDigital.GDPR.CookieConsent.siteStyles.' + site.context.currentSite.nodeName + '.customCSSFilepath'))}
                            siteFile = ${File.readFile(Configuration.setting('KaufmannDigital.GDPR.CookieConsent.siteStyles.' + site.context.currentSite.nodeName + '.customCSSFilepath'))}

                            customFile.@if.noSiteFile = ${!this.siteFile}
                            customFile.@if.isSet = ${String.length(Configuration.setting('KaufmannDigital.GDPR.CookieConsent.customCSSFilepath'))}
                        }
                    }

                    jsVariables = Neos.Fusion:Tag {
                        tagName = 'script'

                        consentLogEnabled = ${Configuration.Setting('KaufmannDigital.GDPR.CookieConsent.consentLogEnabled') ? 'true' : 'false'}

                        trackChoiceUrl = Neos.Fusion:UriBuilder {
                            package = 'KaufmannDigital.GDPR.CookieConsent'
                            controller = 'Api'
                            action = 'trackChoice'
                            absolute = true
                        }

                        generatedJsUrl = Neos.Fusion:UriBuilder {
                            package = 'KaufmannDigital.GDPR.CookieConsent'
                            controller = 'JavaScript'
                            action = 'renderJavaScript'
                            arguments.dimensions = ${site.context.dimensions}
                            absolute = true
                        }
                        content = ${'
                            var trackChoiceUrl = "' + this.trackChoiceUrl + '";
                            var generatedJsUrl = "' + this.generatedJsUrl + '";
                            var consentLogEnabled = ' + this.consentLogEnabled + ';
                        '}
                    }

                    js = Neos.Fusion:Tag {
                        tagName = 'script'
                        content = ${File.readFile('resource://KaufmannDigital.GDPR.CookieConsent/Public/JavaScript/Main.js')}
                    }
                }
            }
        }
    }
}

